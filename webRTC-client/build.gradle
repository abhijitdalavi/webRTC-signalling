plugins {
    id "com.github.node-gradle.node" version "2.2.3"
    id 'base'
}

repositories {
    mavenCentral()
}

npm_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'

    outputs.dir 'build'
}

task packageWebRTC_client(type: Zip) {
    dependsOn npm_run_build
    baseName 'webRTC-client'
    extension 'jar'
    destinationDir file("${projectDir}/build_packageWebRTC_client")
    from('build') {
        into 'static'
    }
}

configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageWebRTC_client.archivePath) {
        builtBy packageWebRTC_client
        type "jar"
    }
}

assemble.dependsOn packageWebRTC_client

clean {
    delete packageWebRTC_client.archivePath.parent
}